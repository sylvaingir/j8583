apply plugin:'java'
apply plugin: 'maven'
defaultTasks 'build'
version='1.15.0-TW-01'
description='Java implementation of the ISO 8583 protocol, focused on making the creation, edition and reading of ISO8583 messages as simple and flexible as possible.'
sourceCompatibility=7
targetCompatibility=7
group='net.sf.j8583'

ext.artifactoryUser = "$System.env.ARTIFACTORY_USER"
ext.artifactoryPassword = "$System.env.ARTIFACTORY_PASSWORD"

repositories {
	mavenCentral()
}

configurations {
	deployerJars
	published.extendsFrom archives, signatures
}

dependencies {
	compile 'org.slf4j:slf4j-api:1.7.21'
	testCompile 'junit:junit:4.12'
	testRuntime 'org.slf4j:slf4j-simple:1.7.21'
}

tasks.javadoc.options.use=true
tasks.javadoc.options.links=['http://docs.oracle.com/javase/7/docs/api/', 'http://slf4j.org/apidocs/' ]

task javadocJar(type:Jar, dependsOn:'javadoc') {
	from javadoc.destinationDir
	classifier='javadoc'
}
task sourcesJar(type:Jar) {
	from sourceSets.main.allSource
	classifier='sources'
}

tasks.withType(AbstractCompile) { AbstractCompile compile ->
	compile.options.debug = true
	compile.options.compilerArgs = ['-Xlint:all']
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

apply plugin:'signing'
signing {
	sign configurations.archives
	required { gradle.taskGraph.hasTask('generateRelease') }
}

uploadArchives {
	repositories {
		mavenDeployer {
			snapshotRepository(url: "http://arti.tw.ee/artifactory/ext-snapshot-local") {
				authentication(userName: System.getenv("ARTIFACTORY_USER") ?: artifactoryUser, password: System.getenv("ARTIFACTORY_PASSWORD") ?: artifactoryPassword)
			}
			repository(url: "http://arti.tw.ee/artifactory/ext-release-local") {
				authentication(userName: System.getenv("ARTIFACTORY_USER") ?: artifactoryUser, password: System.getenv("ARTIFACTORY_PASSWORD") ?: artifactoryPassword)
			}
		}
	}
}

task generateRelease(dependsOn:uploadArchives, group: "Release artifact",
  description: "Generates and uploads a final release to Sonatype Nexus")

